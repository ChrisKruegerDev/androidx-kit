apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

def config = rootProject.ext

archivesBaseName = "placebox-androidx"

install {
    repositories.mavenInstaller {
        pom.project {
            name 'Placebox AndroidX'
            description 'A library with improvements for androidx'
            url 'https://github.com/placeboxapp/placebox-androidx'

            packaging 'aar'
            groupId "com.placebox"
            artifactId "placebox-androidx"
            version '0.1'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/placeboxapp/placebox-androidx.git'
                url 'https://github.com/placeboxapp/placebox-androidx'
            }
            developers {
                developer {
                    name 'chrisbln'
                }
            }
        }
    }
}


bintray {
    user = project.hasProperty('BINTRAY_USER') ? project.property('BINTRAY_USER') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('BINTRAY_API_KEY') ? project.property('BINTRAY_API_KEY') : System.getenv('BINTRAY_API_KEY')

    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = 'placebox-androidx'
        userOrg = "placebox"
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/PlaceboxApp/placebox-androidx'
        vcsUrl = 'https://github.com/PlaceboxApp/placebox-androidx.git'
        publish = true

        version {
            name = '0.1'
            desc = 'Placebox AndroidX version ' + config.versionName
            released  = new Date()
            vcsTag = '0.1'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}

android {
    compileSdkVersion config.compileSdkVersion
    buildToolsVersion config.buildToolsVersion

    useLibrary 'android.test.runner'

    defaultConfig {
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion

        versionName config.versionName
        versionCode config.versionCode

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xallow-result-return-type"]
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

    androidExtensions {
        experimental = true
    }

    lintOptions {
        // for CI server: only check this module with dependencies instead of each module separately
        checkDependencies true
        // for CI server: log reports (report files are not public)
        textReport true
    }

    android {
        lintOptions {
            checkReleaseBuilds false
            // Or, if you prefer, you can continue to check for errors in release builds,
            // but continue the build even when errors are found:
            abortOnError false
        }
    }

    configurations {
        all*.exclude group: 'com.google.guava', module: 'listenablefuture'
    }

}

dependencies {
    Map<String, String> dependencies = config.dependencies

    // kotlin
    implementation dependencies.kotlin
    implementation dependencies.kotlinReflect
}
